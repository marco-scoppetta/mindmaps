insert
#string, long, double, boolean

"entity" isa entity-type, is-abstract; # generic type for a lot of resources
"place" ako entity;
"organization" ako entity;
"language" ako entity;
"tag" ako entity;
"category" ako entity;
"forum" ako entity;

# generic resources
"name" isa resource-type, datatype string;
"creation-date" isa resource-type, datatype string;
"browser-used" isa resource-type, datatype string;
"location-ip" isa resource-type, datatype string;

# a generic relationship for resources of entity
"entity-resource-owner" isa role-type;
"entity-resource-value" isa role-type;
"entity-resource" isa relation-type, has-role entity-resource-value, has-role entity-resource-owner;
entity plays-role entity-resource-owner;
name plays-role entity-resource-value;

# a generic relationship for resources of relation
"relation" isa relation-type, is-abstract;
"relation-resource-owner" isa role-type;
"relation-resource-value" isa role-type;
"relation-resource" isa relation-type, has-role relation-resource-value, has-role relation-resource-owner;
relation plays-role relation-resource-owner;
creation-date plays-role relation-resource-value;

"subject-location" ako location;
"located-subject" isa role-type;
"resides" isa relation-type, has-role subject-location, has-role located-subject;

# place
"continent" ako place;
"country" ako place;
"city" ako place;

"location" isa role-type, is-abstract;

"member-location" ako location;
"container-location" ako location;
"sublocate" isa relation-type, has-role member-location, has-role container-location;
place plays-role location;

# organization
"university" ako organization;
"company" ako organization;
organization plays-role located-subject;

# person
"person" isa entity-type;

"person-resource-owner" isa role-type;
"person-resource-value" isa role-type;
"person-resource" isa relation-type, has-role person-resource-value, has-role person-resource-owner;
person plays-role person-resource-owner;
language plays-role person-resource-value;
creation-date plays-role person-resource-value;
browser-used plays-role person-resource-value;
location-ip plays-role person-resource-value;

"firstname" isa resource-type, datatype string, plays-role person-resource-value;
"lastname" isa resource-type, datatype string, plays-role person-resource-value;
"gender" isa resource-type, datatype string, plays-role person-resource-value;
"birthday" isa resource-type, datatype string, plays-role person-resource-value;
"email" isa resource-type, datatype string, plays-role person-resource-value;
"age" isa resource-type, datatype long, plays-role person-resource-value;

person plays-role located-subject;

"acquaintance" isa role-type, is-abstract;
"acquaintance1" ako acquaintance;
"acquaintance2" ako acquaintance;
"knows" ako relation, has-role acquaintance1, has-role acquaintance2;
person plays-role acquaintance;

"student" isa role-type;
"enrolled-university" isa role-type;
"attends" isa relation-type, has-role student, has-role enrolled-university;
person plays-role student;
university plays-role enrolled-university;

"employee" isa role-type;
"employer" isa role-type;
"employment" isa relation-type, has-role employee, has-role employer;
person plays-role employee;
company plays-role employer;

"class-year" isa resource-type, datatype string;
"attends-resource-value" isa role-type;
"attends-resource-owner" isa role-type;
"attends-resource" isa relation-type, has-role attends-resource-value, has-role attends-resource-owner;
attends plays-role attends-resource-owner;
class-year plays-role attends-resource-value;

"employment-startdate" isa resource-type, datatype string;
"employment-resource-value" isa role-type;
"employment-resource-owner" isa role-type;
"employment-resource" isa relation-type, has-role employment-resource-value, has-role employment-resource-owner;
employment plays-role employment-resource-owner;
employment-startdate plays-role employment-resource-value;

# message
"message" isa entity-type, is-abstract;

"message-resource-owner" isa role-type;
"message-resource-value" isa role-type;
"message-resource" isa relation-type, has-role message-resource-value, has-role message-resource-owner;
message plays-role message-resource-owner;
creation-date plays-role message-resource-value;
browser-used plays-role message-resource-value;
location-ip plays-role message-resource-value;

"content" isa resource-type, datatype string, plays-role message-resource-value;
"length" isa resource-type, datatype string, plays-role message-resource-value;

"liked" isa role-type;
"liker" isa role-type;
"likes" ako relation, has-role liked, has-role liker;
message plays-role liked;
person plays-role liker;

"written" isa role-type;
"writer" isa role-type;
"writes" isa relation-type, has-role written, has-role writer;
message plays-role written;
person plays-role writer;

message plays-role located-subject;

"comment" ako message;

"reply-owner" isa role-type;
"reply-content" isa role-type;
"reply" isa relation-type, has-role reply-content, has-role reply-owner;
comment plays-role reply-content;
message plays-role reply-owner;

"post" ako message;

"post-resource-owner" isa role-type;
"post-resource-value" isa role-type;
"post-resource" isa relation-type, has-role post-resource-owner, has-role post-resource-value;
post plays-role post-resource-owner;
language plays-role post-resource-value;

"image-file" isa resource-type, datatype string, plays-role post-resource-value;

# forum
"forum-resource-owner" isa role-type;
"forum-resource-value" isa role-type;
"forum-resource" isa relation-type, has-role forum-resource-value, has-role forum-resource-owner;
forum plays-role forum-resource-owner;
creation-date plays-role forum-resource-value;

"moderator" isa role-type;
"moderated" isa role-type;
"moderates" isa relation-type, has-role moderator, has-role moderated;
person plays-role moderator;
forum plays-role moderated;

"forum-member" isa role-type;
"membered-forum" isa role-type;
"membership" ako relation, has-role forum-member, has-role membered-forum;
person plays-role forum-member;
forum plays-role membered-forum;

"post-container" isa role-type;
"contained-post" isa role-type;
"containing" isa relation-type, has-role post-container, has-role contained-post;
post plays-role contained-post;
forum plays-role post-container;

# tag
"subject-tag" isa role-type;
"tagged-subject" isa role-type;
"tagging" isa relation-type, has-role tagged-subject, has-role subject-tag;
tag plays-role subject-tag;
forum plays-role tagged-subject;
message plays-role tagged-subject;
person plays-role tagged-subject;

# tag class
"group" isa role-type, is-abstract;
"subgroup" ako group;
"supergroup" ako group;
"subgrouping" isa relation-type, has-role subgroup, has-role supergroup;
category plays-role group;

"grouped-tag" isa role-type;
"tag-group" isa role-type;
"grouping" isa relation-type, has-role grouped-tag, has-role tag-group;
tag plays-role grouped-tag;
category plays-role tag-group;
